@kernel function expand_knot_vector_kernel(
        knots_all,
        @Const(knot_values),
        @Const(multiplicities)
)
    i = @index(Global, Linear)
    knot_value = knot_values[i]

    mult_sum = 0
    idx_end = i - 1
    for j in 1:idx_end
        mult_sum += multiplicities[j]
    end

    idx_start = mult_sum + 1
    idx_end = mult_sum + multiplicities[i]
    for k in idx_start:idx_end
        knots_all[k] = knot_value
    end
end

safe_div(a, b) = iszero(b) ? zero(promote_type(typeof(a), typeof(b))) : a / b

function cox_de_boor(
        basis_function_values, knots_all, t, idx, degree, d, k, deriv
)
    T = eltype(basis_function_values)
    if (k ≤ degree - d) || (k == degree + 2)
        zero(T)
    else
        i = idx + k - degree - 1
        tᵢ = knots_all[i]
        tᵢ₊₁ = knots_all[i + 1]
        tᵢ₊ₚ = knots_all[i + d]
        tᵢ₊ₚ₊₁ = knots_all[i + d + 1]
        if deriv
            T(d * (safe_div(basis_function_values[k], tᵢ₊ₚ - tᵢ) -
               safe_div(basis_function_values[k + 1], tᵢ₊ₚ₊₁ - tᵢ₊₁)))
        else
            T(basis_function_values[k] * safe_div(t - tᵢ, tᵢ₊ₚ - tᵢ) +
              basis_function_values[k + 1] * safe_div(tᵢ₊ₚ₊₁ - t, tᵢ₊ₚ₊₁ - tᵢ₊₁))
        end
    end
end

# Basis function `Bᵢₚ` (Where `p` is the degree) has support on the interval `[tᵢ, tᵢ₊ₚ₊₁)`
# where these `tᵢ` come from `itp_dim.knots_all`. That means that an input `t ∈ [tⱼ, tⱼ₊₁)` (where `j = idx`)
# lies in the support of `Bᵢₚ` for `i = j - p, …, j`, i.e. `p + 1` of the basis functions.
#
# The basis functions are computed recursively using the Cox - de Boor formula in tuples of
# length `p + 2` as follows:
# (0, …, 0, Bⱼ₀, 0) = (0, … 0, 1, 0)
# (0, …, 0, Bⱼ₋₁ ₁, Bⱼ₁, 0)
#          ⋮
# (Bⱼ₋ₚ ₚ, Bⱼ₋ₚ₊₁ ₚ, …, Bⱼₚ, 0)
# 
# The trailing zero is just a convenience for the algorithm and is removed in the output
#
# For type stability of `ntuple` we define degree_plus_one and degree_plus_two 
# in separate function and pass them in as `Val`s. Otherwise type stability is non-trivial.
function get_basis_function_values(
    itp_dim::BSplineInterpolationDimension{degree}, args...) where degree
    get_basis_function_values(itp_dim, Val{degree + 1}(), Val{degree + 2}(), args...)
end
function get_basis_function_values(
        itp_dim::BSplineInterpolationDimension{degree},
        ::Val{degree_plus_one},
        ::Val{degree_plus_two},
        t::Number,
        idx::Integer,
        derivative_order::Integer,
        multi_point_index::Nothing
)::NTuple{degree_plus_one,Float64} where {degree, degree_plus_one, degree_plus_two}
    (; knots_all) = itp_dim

    T = promote_type(typeof(t), eltype(itp_dim.basis_function_eval))
    if derivative_order > degree
        return ntuple(_ -> zero(T), Val{degree_plus_one}())
    end

    # Degree 0 basis function values
    basis_function_values::NTuple{degree_plus_two,Float64} = ntuple(Val{degree_plus_two}()) do k
        # Define T again inside this closure or its type is lost
        T1 = promote_type(typeof(t), eltype(itp_dim.basis_function_eval))
        (k == degree_plus_one) ? oneunit(T1) : zero(T1)
    end

    # Higher order basis function values
    for d in 1:degree
        deriv = d > degree - derivative_order
        basis_function_values = ntuple(Val{degree_plus_two}()) do k
            cox_de_boor(
                basis_function_values, knots_all, t, idx, degree, d, k, deriv
            )
        end
    end

    return ntuple(i -> basis_function_values[i], Val{degree_plus_one}())
end

# Get the basis function values for one point in an
# unstructured multi point evaluation (given by the scalar multi point index)
@noinline function get_basis_function_values(
        itp_dim::BSplineInterpolationDimension,
        t::Number,
        idx::Integer,
        derivative_order::Integer,
        multi_point_index::Integer
)
    view(itp_dim.basis_function_eval,
        multi_point_index, :, derivative_order + 1)
end

# Get all basis function values to evaluate a BSpline interpolation in t
function get_basis_function_values_all(
        A::NDInterpolation,
        ts::Tuple,
        idx::Tuple,
        derivative_orders::Tuple,
        multi_point_index
)
    map(get_basis_function_values, A.interp_dims, ts,
        idx, derivative_orders, multi_point_index)
end

function set_basis_function_eval!(itp_dim::BSplineInterpolationDimension)::Nothing
    backend = get_backend(itp_dim.t_eval)
    basis_function_eval_kernel(backend)(
        itp_dim,
        ndrange = (length(itp_dim.t_eval), itp_dim.max_derivative_order_eval + 1)
    )
    synchronize(backend)
    return nothing
end

@kernel function basis_function_eval_kernel(
        itp_dim
)
    i, derivative_order_plus_1 = @index(Global, NTuple)

    itp_dim.basis_function_eval[i, :, derivative_order_plus_1] .= get_basis_function_values(
        itp_dim,
        itp_dim.t_eval[i],
        itp_dim.idx_eval[i],
        derivative_order_plus_1 - 1,
        nothing
    )
end

# Wrapper for a BSplineInterpolationDimension which acts as a vector of the values
# of the i-th basis function in the `itp_dim.t_eval`. 
struct BasisFunctionVector{ID <: BSplineInterpolationDimension, T} <: AbstractVector{T}
    itp_dim::ID
    i::Int
    derivative_order_eval::Int
    function BasisFunctionVector(itp_dim, i, derivative_order_eval)
        n_basis_functions = get_n_basis_functions(itp_dim)
        @assert 1≤i≤n_basis_functions "The itp_dim has only $n_basis_functions basis functions, got $i."
        @assert 0≤derivative_order_eval≤itp_dim.max_derivative_order_eval "The itp_dim has max_derivative_order_eval = $(itp_dim.max_derivative_order_eval), got $derivative_order_eval."
        new{typeof(itp_dim), eltype(itp_dim.basis_function_eval)}(
            itp_dim, i, derivative_order_eval
        )
    end
end

Base.length(bfv::BasisFunctionVector) = length(bfv.itp_dim.t_eval)
Base.size(bfv::BasisFunctionVector) = (length(bfv),)

function Base.getindex(bfv::BasisFunctionVector, j)
    (; itp_dim, i, derivative_order_eval) = bfv
    (; basis_function_eval, idx_eval, degree) = itp_dim
    idx = idx_eval[j]
    if i ≤ idx ≤ i + degree
        basis_function_eval[j, degree + i - idx + 1, derivative_order_eval + 1]
    else
        zero(eltype(basis_function_eval))
    end
end

function get_n_basis_functions(itp_dim::BSplineInterpolationDimension)
    length(itp_dim.knots_all) - degree(itp_dim) - 1
end

"""
    NURBSWeights(weights::AbstractArray)

Weights associated with the control points to define a NURBS geometry.
"""
struct NURBSWeights{W <: AbstractArray} <: AbstractInterpolationCache
    weights::W
end
